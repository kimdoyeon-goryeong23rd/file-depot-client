plugins {
    id 'java-library'
    id 'maven-publish'
}

group = 'com.saltlux.filedepot'
version = '0.2.0'
description = 'Java SDK for File Depot'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}

tasks.withType(Javadoc).configureEach {
    options.encoding = 'UTF-8'
}

tasks.withType(Test).configureEach {
    systemProperty 'file.encoding', 'UTF-8'
}

repositories {
    mavenCentral()
    maven { url 'https://jitpack.io' }
}

dependencies {
    api 'com.github.agent-hanju:file-depot-api:0.2.0'

    api 'org.springframework:spring-webflux:6.2.7'
    api 'io.projectreactor.netty:reactor-netty-http:1.2.5'

    api 'com.fasterxml.jackson.core:jackson-databind:2.17.2'
    api 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.17.2'
    api 'org.slf4j:slf4j-api:2.0.16'

    compileOnly 'org.projectlombok:lombok:1.18.34'
    annotationProcessor 'org.projectlombok:lombok:1.18.34'

    testImplementation 'org.junit.jupiter:junit-jupiter:5.11.3'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
    testImplementation 'org.testcontainers:testcontainers:1.20.4'
    testImplementation 'org.testcontainers:junit-jupiter:1.20.4'
    testImplementation 'org.assertj:assertj-core:3.26.3'
    testImplementation 'org.springframework:spring-context:6.2.7'

    testRuntimeOnly 'ch.qos.logback:logback-classic:1.5.12'
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java

            pom {
                name = "File Depot Client"
                description = project.description
                url = 'https://github.com/kimdoyeon-goryeong23rd/file-depot-client'

                licenses {
                    license {
                        name = 'Saltlux Internal License'
                        url = 'https://github.com/kimdoyeon-goryeong23rd/file-depot-client/blob/main/LICENSE'
                    }
                }

                developers {
                    developer {
                        id = 'kimdoyeon-goryeong23rd'
                        name = 'Kim Doyeon'
                        email = 'doyeon.kim2@saltlux.com'
                    }
                }

                scm {
                    connection = 'scm:git:git://github.com/kimdoyeon-goryeong23rd/file-depot-client.git'
                    developerConnection = 'scm:git:ssh://github.com/kimdoyeon-goryeong23rd/file-depot-client.git'
                    url = 'https://github.com/kimdoyeon-goryeong23rd/file-depot-client'
                }
            }
        }
    }
}

tasks.named('test') {
    useJUnitPlatform()
}
